# runs the ui for the client
window.NimbusApp.UI = (socket_uri, initial_directory) ->
  show_spinner()
  $(window).resize -> NimbusApp.UI.adjust_files_scroll()
  extensions_map =
    txt: 'text', epub: 'application-book', mobi: 'application-book', azw: 'application-book',
    zip: 'package-x-generic', rar: 'package-x-generic', '7z': 'package-x-generic', cab: 'package-x-generic',
    gz: 'package-x-generic', bz2: 'package-x-generic', tar: 'package-x-generic', ai: 'application-illustrator',
    doc: 'application-msword', docx: 'application-msword', rtf: 'office-document', accdb: 'application-vnd.ms-access',
    mdb: 'application-vnd.ms-access', xls: 'application-vnd.ms-excel', xlsx: 'application-vnd.ms-excel',
    ppt: 'application-vnd.ms-powerpoint', pptx: 'application-vnd.ms-powerpoint', odt: 'office-document',
    odf: 'office-document', card: 'office-contact', torrent: 'application-x-bittorrent',
    iso: 'application-x-cd-image', nrg: 'application-x-cd-image', mdf: 'application-x-cd-image',
    flv: 'application-x-flash-video', fla: 'application-x-flash-video', swf: 'application-x-flash-video',
    exe: 'application-x-ms-dos-executable', msi: 'application-x-ms-dos-executable', mp3: 'audio-x-generic',
    aac: 'audio-x-generic', m4a: 'audio-x-generic', ogg: 'audio-x-generic', flac: 'audio-x-generic',
    wav: 'audio-x-generic', opus: 'audio-x-generic', wma: 'audio-x-generic', ape: 'audio-x-generic',
    png: 'image-x-generic', bmp: 'image-x-generic', jpg: 'image-x-generic', jpeg: 'image-x-generic',
    gif: 'image-x-generic', tif: 'image-x-generic', tiff: 'image-x-generic', webp: 'image-x-generic',
    targa: 'image-x-generic', raw: 'image-x-generic', exr: 'image-x-generic', tga: 'image-x-generic',
    hdr: 'image-x-generic', m3u: 'playlist', cue: 'playlist', htm: 'text-html', html: 'text-html', xhtml: 'text-html',
    mhtm: 'text-html', dmg: 'text-x-install', rpm: 'text-x-install', deb: 'text-x-install', nfo: 'text-x-readme',
    odg: 'x-office-drawing', otg: 'x-office-drawing', svg: 'x-office-drawing', odp: 'x-office-presentation',
    ods: 'x-office-spreadsheet', ots: 'x-office-spreadsheet', csv: 'x-office-spreadsheet', tsv: 'x-office-spreadsheet',
    ini: 'application-x-desktop', xml: 'application-x-desktop', conf: 'application-x-desktop',
    cnf: 'application-x-desktop', js: 'text-x-script', coffee: 'text-x-script', c: 'text-x-script',
    cpp: 'text-x-script', cs: 'text-x-script', rb: 'text-x-script', java: 'text-x-script', jar: 'text-x-java',
    'class': 'application-x-executable', scala: 'text-x-script', py: 'text-x-script', pyc: 'application-x-executable',
    css: 'text-x-script', sh: 'text-x-script', pl: 'text-x-script', ott: 'x-office-document-template',
    pdf: 'application-pdf', mp4: 'video-x-generic', m4v: 'video-x-generic', avi: 'video-x-generic',
    wmv: 'video-x-generic', mkv: 'video-x-generic', vob: 'video-x-generic', webm: 'video-x-generic'

  nimbus = NimbusApp.Core(socket_uri, refresh);
  topbar = null
  files_body = null
  modal = null
  init_done = $.Deferred();
  init_done.fail (error) ->
    stop_spinner()
    $('#app-container').html(error || 'Something went wrong')
  init_done.done ->
    window.onpopstate = (event) ->
     promise = $.Deferred()
     promise.fail (reason) -> console.log(reason)
     promise.done (directory) -> change_directory(directory, true)
     nimbus.get_directory(event.state, promise)
    dir = nimbus.current_directory()
    history.replaceState(dir.path(), dir.name(), '')
    create_ui()
    refresh()
  nimbus.initialize(init_done, initial_directory)

  #initializes UI
  create_ui = ->
    app = $('#app-container')
    topbar = $('<div class="panel" id="menubar"></div>')
    files_body = $('<tbody id="files-body">')
    table = $('<table id="files-table">')
    scroll_container = $('<div id="files-scroll"></div>')
    modal = $('<div class="mfp-hide popup"></div>')
    table.html(files_body)
    scroll_container.append(table)
    app.append(topbar)
    app.append(scroll_container)
    app.append(modal)
    NimbusApp.UI.adjust_files_scroll()

  #refresh callback
  refresh = ->
    new_bar = create_menubar(nimbus.current_directory())
    topbar.replaceWith(new_bar)
    topbar = new_bar
    files_body.empty()
    folders = nimbus.current_directory().subdirectories()
    for f in folders
      files_body.append(create_row(f))
    files = nimbus.current_directory().files();
    for f in files
      files_body.append(create_row(f))
    stop_spinner()

  # Changes the directory and refreshes
  change_directory = (directory, ignore_state) ->
    promise = $.Deferred()
    promise.done ->
        history.pushState(directory.path(), directory.name(), '') unless ignore_state
        refresh()
    promise.fail (error) -> alert(error)
    nimbus.change_directory(directory, promise)
    show_spinner() unless directory.is_enumerated()

  # Creates a context menu around the mouse
  context_menu = (event, file, row) ->
    body = $('body')
    # Handle touch events
    if event.type == 'touchend'
      event = event.originalEvent.changedTouches[0]
    menu = menu_contents(file)
    x = event.clientX
    y = event.clientY
    # Change corner at edges
    body.append(menu)
    width = menu.width()
    height = menu.height()
    hor_margin = $(window).width() - (width + x)
    vert_margin = $(window).height() - (height + y)
    x -= width if hor_margin < 50
    y -= height if vert_margin < 50
    x = Math.max(x, 0)
    y = Math.max(y, 0)
    menu.css(left: x, top: y)
    # Handle exiting menu
    func = (event) ->
      # Handle touch events
      if  event.type == 'touchstart'
        event = event.originalEvent.changedTouches[0]
      click_x = event.clientX
      click_y = event.clientY
      outside_x = click_x < x or click_x > x + width
      outside_y = click_y < y or click_y > y + height
      if outside_x or outside_y
        menu.remove()
        row.removeClass('selected-row')
        body.off('mousedown touchstart', func)
    body.on('mousedown touchstart',  func)
    $('#files-scroll').scroll ->
      menu.remove()
      row.removeClass('selected-row')
      $('body').off('mousedown touchstart', func)

  #Creates the breadcrumbs
  create_breadcrumbs = (metaDirectory) ->
    breadcrumbs = $('<nav class="breadcrumbs"></nav>')
    crumb = null
    while(metaDirectory != null)
      if crumb == null and metaDirectory.parent() == null
        crumb = $('<a class="current">Root</a>')
      else if crumb == null
        crumb = $('<a class="current">' + metaDirectory.name() + '</a>')
      else if(metaDirectory.parent() == null )
        crumb = $('<a>Root</a>')
      else
        crumb = $('<a>' + metaDirectory.name() + '</a>')
      # each iteration shares a common scope, we must make a new one
      do ->
        promise = $.Deferred()
        promise.done ->
          refresh()
        promise.fail (error) ->
          alert(error)
        dir = metaDirectory
        crumb.click ->
          change_directory(dir)
      breadcrumbs.prepend(crumb)
      metaDirectory = metaDirectory.parent()
    return breadcrumbs

  # Creates a row for a file or folder
  create_row = (file) ->
    row = $("<tr></tr>")
    row.on 'contextmenu', (event) ->
      context_menu(event, file, row)
      event.preventDefault()
      row.addClass('selected-row')
    row.append icon_column(file)
    row.append name_column(file)
    row.append date_column(file)
    row.append size_column(file)
    menu = menu_button(file, row)
    menu.on 'mouseup touchend', ->
      func = -> row.addClass('selected-row')
      #deal with menu button being reselected (remove and then readd class)
      setTimeout(func, 5)
    row.append(menu)
    return row

  # creates the menu bar in the topbar
  create_menubar = (file) ->
      menubar = $('<div class="panel" id="menubar"></div>')
      menubar.append(create_breadcrumbs(file))
      actions = $('<div id="menu-actions"></ul>')
      refresh_img = $('<img alt="refresh" src="<%= image_path 'refresh.svg' %>">')
      refresh_button = $('<a>').append(refresh_img)
      refresh_button.on  'mousedown touchstart', ->
          refresh_img.css(opacity: 1)
      refresh_button.on 'mouseup touchend' , ->
          transition_opacity(refresh_img)
          show_spinner()
          promise = $.Deferred()
          promise.done -> refresh()
          file.update(promise)
      actions.append(refresh_button)
      actions.append $('<a><img alt="upload" src="<%= image_path 'upload.svg' %>"></a>')
      actions.append $('<a><img alt = "settings" src="<%= image_path 'settings.svg' %>"></a>')
      menubar.append(actions)
      return menubar

  # Creates a column for modified date
  date_column = (file) ->
    if file.is_file()
      date = new Date(file.time())
      hours = date.getHours()
      suffix = if hours < 12 then 'AM' else 'PM'
      hours %= 12
      hours = 12 if hours == 0
      minutes = date.getMinutes()
      minutes++ if date.getSeconds() > 30
      minute_string = if minutes < 10 then '0' + minutes else minutes + ''
      timeString = hours + ':' + minute_string + suffix
      dateString = date.getMonth() + '/' + date.getDay() + '/' + date.getFullYear()
      $("<td class='date-column'>" + timeString + " " + dateString + "</td>")
    else
      $("<td class='date-column'>--</td> ")

  # Shows modal to confirm deletion of a file
  delete_dialog = (file) ->
      header = $('<header><h3>Delete File</h3></header>')
      body = $('<p>Are you sure you would like to delete this file?<p>')
      delete_button = $('<button class="alert button">Delete</button>')
      delete_button.click ->
          promise = $.Deferred()
          promise.done ->
            internal_promise = $.Deferred()
            internal_promise.done -> refresh()
            internal_promise.fail (reason) -> console.log(reason)
            nimbus.current_directory().update(internal_promise)
          promise.fail -> console.log('unable to delete')
          file.destroy(promise)
          $.magnificPopup.close()
          show_spinner()
      footer = $('<footer>')
      footer.append(delete_button)
      modal.html(header)
      modal.append(body)
      modal.append(footer)
      show_modal()

  # Creates column for a file link
  file_column = (file) ->
    data = $("<td class='filename'></td>")
    is_image = false
    if ["png", "gif", "jpg", "jpeg", "bmp"].indexOf(file.extension().toLowerCase()) != -1
      is_image = true
    url = null
    link = $('<a>' + file.full_name() + '</a>')
    data.html(link)
    if(file.hasOwnProperty("download_url"))
      url = file.download_url()
    else if(file.hasOwnProperty("view_url"))
      url = file.view_url()
    if is_image
      data.attr(href: url)
      data.magnificPopup
        mainClass: 'modal',
        type: 'image',
        removalDelay: 200
    else
      data.click -> window.location = url
    return data

  # Creates column for a folder link
  folder_column = (file) ->
    link = $("<a>" + file.name() + "</a>")
    folder = $("<td class='filename'></td>")
    folder.click -> change_directory(file)
    folder.html(link)
    return folder

  # Create column for icon
  icon_column = (file) ->
    suffix = Math.max(Math.min(Math.round(devicePixelRatio), 3), 1) * 24
    is_file = file.is_file()
    if is_file and extensions_map.hasOwnProperty(file.extension().toLowerCase())
      $("<td class='icon'><img height='32' width='32' alt='icon' src='/icons/" +
        extensions_map[file.extension().toLowerCase()] + suffix + ".png'></td>")
    else if is_file
      $("<td class='icon'><img height='32' width='32' alt='icon' src='/icons/blank" + suffix + ".png'></td>")
    else
      $("<td class='icon'><img height='32' width='32' alt='icon' src='/icons/folder" + suffix + ".png'></td>")

  # Creates column for menu button
  menu_button = (file, row) ->
    button = $('<td class="menu-button"><a><svg xmlns="http://www.w3.org/2000/svg" height="24" width="12">' +
      '<g class="menu-svg"><rect height="4" width="4" y="2" x="5"/><rect height="4" width="4" y="10" x="5"/>'+
      '<rect height="4" width="4" y="18" x="5"/></g></svg></a></td>')
    button.on 'mouseup touchend', (event) ->
      context_menu(event, file, row)
    return button

  # Creates the contents of a context menu
  menu_contents = (file) ->
    div = $('<div class="context-menu"></div>')
    list = $("<ul></ul>")
    del = $("<li>Delete</li>")
    del.click -> delete_dialog(file)
    rename = $("<li>Rename</li>")
    list.append del
    list.append rename
    div.html list
    return div

  # Create column for file/folder name/link
  name_column = (file) ->
    if file.is_file()
      file_column(file)
    else
      folder_column(file)

  #shows the modal
  show_modal = ->
    $.magnificPopup.open
      items:
        src: modal
        type: 'inline'
      mainClass: 'modal'
      tLoading: ''
      removalDelay: 200
      closeBtnInside: true

  # Create column for size
  size_column = (file) ->
    sizeString = (size) ->
      iter = 0
      suffixes = ['B', 'KB', 'MB', 'GB', 'TB']
      while(size > 1024)
        size /= 1024
        iter++
      return Math.round(size) + ' ' + suffixes[iter]
    if file.is_file() and !isNaN(file.size())
      $("<td class='size-column'>" + sizeString(file.size()) + "</td>")
    else
      $("<td class='size-column'>--</td>")

  transition_opacity = (element) ->
      element.fadeTo(200, 0.3)

#Adjust scroll area
window.NimbusApp.UI.adjust_files_scroll = ->
  $('#files-scroll').height($(window).height() - 125)
